apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

archivesBaseName = getArchivesBaseName(project.name)
version = rootProject.ext.mdcLibraryVersion

dependencies {
  api compatibility("annotations")
  api compatibility("compat")
  api compatibility("core_ui")
  api compatibility("core_utils")
  api compatibility("fragment")
  api compatibility("transition")
  api compatibility("v7/appcompat")
  api compatibility("v7/recyclerview")

  api project(fromPath("lib/src/android/support/design/animation"))
  api project(fromPath("lib/src/android/support/design/circularreveal"))
  api project(fromPath("lib/src/android/support/design/circularreveal/cardview"))
  api project(fromPath("lib/src/android/support/design/math"))
  api project(fromPath("lib/src/android/support/design/resources"))
  api project(fromPath("lib/src/android/support/design/ripple"))
  api project(fromPath("lib/src/android/support/design/stateful"))

  testImplementation("com.android.support.test:runner:${project.rootProject.ext.testRunnerVersion}") {
    exclude module: 'support-annotations'
  }
  testImplementation "junit:junit:4.12"
  testImplementation "com.google.truth:truth:0.35"
  testImplementation "org.mockito:mockito-core:1.9.5"
  testImplementation "org.robolectric:robolectric:3.3.2"
}

android {
  sourceSets {
    main.manifest.srcFile 'AndroidManifest.xml'
    main.java.srcDir 'src'
    main.java.includes = [
      'android/support/design/internal/*.java',
      'android/support/design/widget/**/*.java',
    ]
    main.java.excludes = [
      '**/build/**',
    ]
    main.res.srcDirs = [
      'res',
      'res-public'
    ]
    main.assets.srcDir 'assets'
    main.resources.srcDir 'src'

    test.java.srcDir 'jvmtests/javatests'
  }

  buildTypes.all {
    consumerProguardFiles 'proguard-rules.pro'
  }
}

uploadArchives {
  repositories {
    mavenDeployer {
      repository(url: rootProject.ext.mavenRepoUrl)
    }
  }
}
